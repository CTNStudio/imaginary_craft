plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.139'
    id "org.jetbrains.kotlin.jvm" version "2.0.0"
    id "org.jetbrains.kotlin.plugin.serialization" version "2.0.0"
    id 'idea'
}

tasks.named('wrapper', Wrapper).configure {
    // Define wrapper values here so as to not have to always do so when updating gradlew.properties.
    // Switching this to Wrapper.DistributionType.ALL will download the full gradle sources that comes with
    // documentation attached on cursor hover of gradle classes and methods. However, this comes with increased
    // file size for Gradle. If you do switch this to ALL, run the Gradle wrapper task twice afterwards.
    // (Verify by checking gradle/wrapper/gradle-wrapper.properties to see if distributionUrl now points to `-all`)
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id


repositories {
  maven {
    url = "https://libraries.minecraft.net/"
  }

  maven {
    url = "https://neoforged.forgecdn.net/releases"
  }

  maven {
    url = "https://neoforged.forgecdn.net/mojang-meta"
  }

  mavenLocal()
  mavenCentral()

  maven {
    name = "NeoForged"
    url = "https://neoforged.forgecdn.net/releases"
  }

  // KotlinForForge
  maven {
    name = "Kotlin for Forge"
    url = "https://thedarkcolour.github.io/KotlinForForge/"
    content { includeGroup "thedarkcolour" }
  }

  // Jitpack
  maven {
    name = "JitPack"
    url = 'https://jitpack.io'
  }

  // Modrinth
  maven {
    url = uri("https://api.modrinth.com/maven")
  }

  // BlameJared
  maven {
    url = uri("https://maven.blamejared.com/")
  }

  // CurseForge
  maven {
    url = uri("https://www.cursemaven.com")
    content { includeGroup "curse.maven" }
  }

  // GeckoLib
  maven {
    url = uri("https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/")
    content { includeGroup("software.bernie.geckolib") }
  }

  // Kosmx 的个人仓库
  maven {
    url = uri("https://maven.kosmx.dev/")
  }

  // Bernie's 仓库（部分模组依赖）
  maven {
    url = uri("https://maven.theillusivec4.top/")
  }

  maven {
    name = "RedlanceMinecraft"
    url = "https://repo.redlance.org/public"
  }
}

base {
    archivesName = mod_id
}

// Mojang ships Java 21 to end users in 1.21.1, so mods should target Java 21.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    // Specify the version of NeoForge to use.
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    // This line is optional. Access Transformers are automatically detected
    // accessTransformers = project.files('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            client()

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        // This run config launches GameTestServer and runs all registered gametests, then exits.
        // By default, the server will crash when no gametests are provided.
        // The gametest system is also enabled by default for other run configs under the /test command.
        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()

            // example of overriding the workingDirectory set in configureEach above, uncomment if you want to use it
            // gameDirectory = project.file('run-data')

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        // applies to all the run configs above
        configureEach {
            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            systemProperty 'forge.logging.console.level', 'debug'

            jvmArguments.addAll([
              // 忽略无效指令，避免有的人没安装 JetBrain Runtime 无法启动游戏
              "-XX:+IgnoreUnrecognizedVMOptions",
              // 启用 JetBrain Runtime 热重载功能
              "-XX:+AllowEnhancedClassRedefinition"
            ])

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        // define mod <-> source bindings
        // these are used to tell the game which sources are for which mod
        // multi mod projects should define one per mod
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

// Sets up a dependency configuration called 'localRuntime'.
// This configuration should be used instead of 'runtimeOnly' to declare
// a dependency that will be present for runtime testing but that is
// "optional", meaning it will not be pulled by dependents of this mod.
configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
  implementation "net.neoforged:neoforge:${neo_version}"
  implementation "thedarkcolour:kotlinforforge-neoforge:${kff_version}"

  jarJar(implementation(group: "club.snowlyicewolf", name: "amarok-json-for-java")) {
    version { prefer aj4j_version }
  }
  jarJar(implementation(group: "club.snowlyicewolf", name: "amarok-kdl-for-java")) {
    version { prefer ak4j_version }
  }

//  libraries "club.snowlyicewolf:amarok-json-for-java:${aj4j_version}"
//  libraries "club.snowlyicewolf:amarok-kdl-for-java:${ak4j_version}"
  api "club.snowlyicewolf:amarok-json-for-java:${aj4j_version}"
  api "club.snowlyicewolf:amarok-kdl-for-java:${ak4j_version}"

  compileOnly "org.jetbrains.kotlin:kotlin-stdlib"
  compileOnly "org.jetbrains.kotlin:kotlin-reflect"
  compileOnly "org.jetbrains.kotlinx:kotlinx-coroutines-core"

  implementation "software.bernie.geckolib:geckolib-neoforge-${minecraft_version}:${geckolib_version}"

  implementation "maven.modrinth:jade:${jade_version}+neoforge"
  compileOnly "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"
  compileOnly "mezz.jei:jei-${minecraft_version}-neoforge-api:${jei_version}"
  localRuntime "mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}"

  implementation "top.theillusivec4.curios:curios-neoforge:${curios_api_version}"
  implementation "top.theillusivec4.curios:curios-neoforge:${curios_api_version}:api"

//  implementation "com.zigythebird.playeranim:PlayerAnimationLibNeo:${pal_version}"
  compileOnly "com.zigythebird.playeranim:PlayerAnimationLibNeo:${pal_version}"
  localRuntime "org.javassist:javassist:3.30.2-GA"
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
        minecraft_version      : minecraft_version,
        minecraft_version_range: minecraft_version_range,
        neo_version            : neo_version,
        loader_version_range   : loader_version_range,
        mod_id                 : mod_id,
        mod_name               : mod_name,
        mod_license            : mod_license,
        mod_version            : mod_version,
        mod_authors            : mod_authors,
        mod_description        : mod_description,
        geckolib_version       : geckolib_version,
        curios_api_version     : curios_api_version,
        pal_version            : pal_version
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}
// Include the output of "generateModMetadata" as an input directory for the build
// this works with both building through Gradle and the IDE.
sourceSets.main.resources.srcDir generateModMetadata
// To avoid having to run "generateModMetadata" manually, make it run on every project reload
neoForge.ideSyncTask generateModMetadata

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
